Set_InsertNotFound:

    ; Root Node             Root Node (modified)                   # end of tree
    ;  / \                   / \                                   |
    ;    Intermediate Node      Intermediate Node (modified)       ^ climb tree
    ;     /                      / \                               |
    ;                       --->    New Value Node (new)           * start here


    ; We have not found a leaf node in the path to the key we want to insert
    ; We have to create a new leaf node (new value's) at the point when the key diverges with any other key
    ; and then, climb the tree until the top, i.e. until the root

    ; At this point, value isn't zero, it's verified in storage_sm.zkasm

    ; Create the a new leaf nodes for the new value

    ; NewValueHash = Hash0( VALUE_LOW, VALUE_HIGH )
    VALUE_LOW => HASH_LEFT
    VALUE_HIGH => HASH_RIGHT

    $ => HASH_RIGHT                 :HASH0

    ; NewRoot = LeafNodeHash = Hash1( RKEY, Hash0( VALUE_LOW, VALUE_HIGH ) )
    ${GetRkey()} => RKEY, HASH_LEFT
    $ => NEW_ROOT                   :HASH1

SINF_InitLevel:

    ; Init LEVEL register
    ${GetLevel()} => LEVEL

    ; Create the first intermediate node that joins the old and the new value's leaf nodes

SINF_StartOldRoot:
    ; OldRoot = sibling Hash
    0x0 => OLD_ROOT

SINF_ClimbTree:

    ; If we are at the top of the tree, then go to SINF_Latch
    LEVEL                           :JMPZ(SINF_Latch)
    LEVEL - 1 => LEVEL

    ; If next key bit is zero, then the sibling hash must be at the right (sibling's key bit is 1)
    ${GetNextKeyBit()} => RKEY_BIT  :JMPZ(SINF_SiblingIsRight3)

SINF_SiblingIsLeft3:

    ; NewRoot = Hash0 ( SiblingHash, NewRoot )
    ${GetSiblingHash()} => HASH_LEFT
    NEW_ROOT => HASH_RIGHT

    $ => NEW_ROOT                   :HASH0

    ; OldRoot = Hash0 ( SiblingHash, OldRoot )
    OLD_ROOT => HASH_RIGHT
    $ => OLD_ROOT                   :HASH0

    ; Update remaining key
    $ => RKEY                       :CLIMB_RKEY, JMP(SINF_ClimbTree)

SINF_SiblingIsRight3:

    ; NewRoot = Hash0 ( NewRoot, SiblingHash )
    NEW_ROOT => HASH_LEFT
    ${GetSiblingHash()} => HASH_RIGHT
    $ => NEW_ROOT                   :HASH0

    ; OldRoot = Hash0 ( OldRoot, SiblingHash )
    OLD_ROOT => HASH_LEFT
    $ => OLD_ROOT                   :HASH0

    ; Update remaining key
    $ => RKEY                       :CLIMB_RKEY, JMP(SINF_ClimbTree)

SINF_Latch:

    ; At this point consistency is granted: OLD_ROOT, NEW_ROOT, RKEY (complete key), VALUE_LOW, VALUE_HIGH, LEVEL
    ; Return to the main loop
                                    :LATCH_SET, JMP(Run)