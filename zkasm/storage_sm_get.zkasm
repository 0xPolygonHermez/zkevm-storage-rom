Get:

    ; Root Node (same as before)                # end of tree
    ;  / \                                      |
    ;     Intermediate Node (same as before)    ^ climb tree
    ;      / \                                  |
    ;         Leaf Value Node (same as before)  * start here

    ; During a get operation we have 2 different cases:
    ;
    ; 1.- We found the key and a non-zero value
    ;     We have to create the value hash entry and the corresponding leaf node
    ;     that will be the first value of old root, and then climb the tree up to the top
    ;
    ; 2.- We could not find the key, and therefore value is zero. In this situation, there are
    ;     two possibilities, we start the search on an intermediate node (2A) or on a leaf node.
    ;
    ;     2A.- We have to start with old root = 0, and then climb the tree up to the top
    ;          In case we are already at the top (no siblings) the old root will remain 0
    ;
    ;     2B.- We have to start with hash of leaf, to calculate hash need to use values of
    ;          remaining key and value of the leaf node.


    ; Get the remaining key
    ${GetRkey()} => RKEY

    ; Get the retrieved value
    ; If value is 0, skip the leaf node creation and go to Get_ValueIsZero

    ${GetValueLow()} => VALUE_LOW, VALUE_HIGH :JMPZ(Get_VL0IsZero)
                                              :JMP(Get_VLIsNotZero)

    ; Value is zero if and only if VL and VH are zero
    ; VL is zero if and only if VL0,VL1,VL2,VL3 are zero
Get_VL0IsZero:
    ROTL_VH => VALUE_HIGH       :JMPZ(Get_VL01IsZero)
                                :JMP(Get_VLIsNotZero)

    ; VL0,VL1 are zero
Get_VL01IsZero:
    ROTL_VH => VALUE_HIGH       :JMPZ(Get_VL012IsZero)
                                :JMP(Get_VLIsNotZero)

    ; VL0,VL1,VL2 are zero
Get_VL012IsZero:
    ROTL_VH => VALUE_HIGH       :JMPZ(Get_VLIsZero)
                                :JMP(Get_VLIsNotZero)

    ; VL0,VL1,VL2,VL3 are zero, means VL is zero
Get_VLIsZero:
    ${GetValueHigh()} => VALUE_HIGH :JMPZ(Get_VLVH0IsZero)
                                    :JMP(Get_ValueIsNotZero)

    ; VL,VH0 are zero
Get_VLVH0IsZero:
    ROTL_VH => VALUE_HIGH       :JMPZ(Get_VLVH01IsZero)
    ROTL_VH => VALUE_HIGH
    ROTL_VH => VALUE_HIGH
    ROTL_VH => VALUE_HIGH       :JMP(Get_ValueIsNotZero)

Get_VLVH01IsZero:
    ROTL_VH => VALUE_HIGH        :JMPZ(Get_VLVH012IsZero)
    ROTL_VH => VALUE_HIGH
    ROTL_VH => VALUE_HIGH        :JMP(Get_ValueIsNotZero)

Get_VLVH012IsZero:
    ROTL_VH => VALUE_HIGH        :JMPZ(Get_ValueIsZero)
    ROTL_VH => VALUE_HIGH        :JMP(Get_ValueIsNotZero)

    ; VL isn't zero, but need to read VALUE_HIGH because finished to compare
    ; registers before VALUE_HIGH was read
Get_VLIsNotZero:
    ${GetValueHigh()} => VALUE_HIGH

Get_ValueIsNotZero:

    ; Create the value hash and the leaf node hash, which will be the initial value of old root

    ; ValueHash = Hash0( VALUE_LOW, VALUE_HIGH )
    VALUE_LOW => HASH_LEFT
    VALUE_HIGH => HASH_RIGHT
    $ => HASH_RIGHT                 :HASH0

    ; OldRoot = LeafNodeHash = Hash1( Rkey, Hash( VALUE_LOW, VALUE_HIGH ) )
    RKEY => HASH_LEFT
    $ => OLD_ROOT                   :HASH1

                                    :JMP(Get_InitLevel)

Get_ValueIsZero:

    ${GetIsOld0()}                  :JMPZ(Get_ValueIsZero_LeafNode)
                                    :JMP(Get_ValueIsZero_IntermediateNode)

Get_ValueIsZero_LeafNode:

    ; How was a leaf node, will calculate hash of leaf.

    ${GetSiblingValueLow()} => HASH_LEFT
    ${GetSiblingValueHigh()} => HASH_RIGHT
    $ => HASH_RIGHT                 :HASH0

    ${GetSiblingRkey()} => HASH_LEFT
    $ => OLD_ROOT                   :HASH1
                                    :JMP(Get_InitLevel)

Get_ValueIsZero_IntermediateNode:

    ; No need to calculate the hash; the next intermediate node will use simply 0 as a hash
    0x0 => OLD_ROOT


Get_InitLevel:

    ; Init LEVEL register to {1,0,0,0}, and inversely rotate it level%4 times,
    ; so that after level rotations the position of LEVEL becomes {1,0,0,0} again
    0x1 => LEVEL

    ; If level bit 0 is 1, then do 1 inverse rotation (i.e. 3 normal rotations)
    ${GetLevelBit(0)}               :JMPZ(Get_LevelBit1)
                                    :ROTATE_LEVEL
                                    :ROTATE_LEVEL
                                    :ROTATE_LEVEL

Get_LevelBit1:

    ; If level bit 1 is 1, then do 2 inverse rotations (i.e. 2 normal rotations)
    ${GetLevelBit(1)}               :JMPZ(Get_ClimbTree)
                                    :ROTATE_LEVEL
                                    :ROTATE_LEVEL

Get_ClimbTree:

    ; If we are at the top of the tree, then goto Get_Latch
    ${GetTopTree()}                 :JMPZ(Get_Latch)

    ; If next key bit is zero, then the sibling hash must be at the right (sibling's key bit is 1)
    ${GetNextKeyBit()} => RKEY_BIT
    RKEY_BIT                        :JMPZ(Get_SiblingIsRight)

Get_SiblingIsLeft:

    ; OldRoot = Hash0( SiblingHash, OldRoot );
    ${GetSiblingHash()} => HASH_LEFT
    OLD_ROOT => HASH_RIGHT
    $ => OLD_ROOT                   :HASH0

    ; Update remaining key
                                    :ROTATE_LEVEL
                                    :CLIMB_RKEY

                                    :JMP(Get_ClimbTree)

Get_SiblingIsRight:

    ; OldRoot = Hash0( OldRoot, SiblingHash );
    OLD_ROOT => HASH_LEFT
    ${GetSiblingHash()} => HASH_RIGHT
    $ => OLD_ROOT                   :HASH0

    ; Update remaining key
                                    :ROTATE_LEVEL
                                    :CLIMB_RKEY

                                    :JMP(Get_ClimbTree)

Get_Latch:

    ; At this point consistency is granted: OLD_ROOT, RKEY (complete key), VALUE_LOW, VALUE_HIGH, LEVEL
                                    :LATCH_GET

    ; Return to the main loop
                                    :JMP(Run)